plugins {
	id 'java'
	id 'groovy'
	id 'idea'

	id 'org.springframework.boot' version '2.5.0'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id "com.coditory.integration-test" version "1.2.1"

	id 'com.palantir.docker' version '0.26.0'

	id 'com.google.protobuf' version '0.8.16'
}

group = 'pl.oxerek'
version = '0.0.1-SNAPSHOT'

sourceCompatibility = JavaVersion.VERSION_15
targetCompatibility = JavaVersion.VERSION_15

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-validation'

	implementation 'io.grpc:grpc-netty-shaded:1.38.0'
	implementation 'io.grpc:grpc-protobuf:1.38.0'
	implementation 'io.grpc:grpc-stub:1.38.0'

	implementation 'net.devh:grpc-client-spring-boot-starter:2.12.0.RELEASE'
	implementation 'net.devh:grpc-spring-boot-starter:2.12.0.RELEASE'

	implementation 'com.salesforce.servicelibs:reactor-grpc-stub:1.0.1'

	implementation 'org.mapstruct:mapstruct:1.4.2.Final'

	implementation 'io.vavr:vavr:0.10.3'

	implementation 'org.apache.commons:commons-lang3:3.12.0'
	implementation 'org.apache.commons:commons-csv:1.8'

	implementation 'com.hazelcast:hazelcast:4.2'

	implementation 'io.projectreactor.tools:blockhound:1.0.6.RELEASE'

	compileOnly 'org.projectlombok:lombok'

	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	annotationProcessor 'org.projectlombok:lombok'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.4.2.Final'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'

	testImplementation platform('org.spockframework:spock-bom:2.0-groovy-3.0')
	testImplementation 'org.spockframework:spock-core'
	testImplementation 'org.spockframework:spock-spring'

	testImplementation 'org.codehaus.groovy:groovy-all:3.0.7'
	testImplementation 'com.tngtech.archunit:archunit:0.18.0'
	//testImplementation 'io.projectreactor.tools:blockhound:1.0.6.RELEASE'

	testImplementation platform('org.testcontainers:testcontainers-bom:1.15.3')
	testImplementation('org.testcontainers:spock')
}

ext['groovy.version'] = '3.0.7'

protobuf {
	protoc {
		artifact = "com.google.protobuf:protoc:3.17.3"
	}
	plugins {
		grpc {
			artifact = "io.grpc:protoc-gen-grpc-java:1.38.0"
		}
		reactor {
			artifact = "com.salesforce.servicelibs:reactor-grpc:1.0.1"
		}
	}
	generateProtoTasks {
		ofSourceSet("main")*.plugins {
			grpc {}
			reactor {}
		}
	}
}

sourceSets {
	main {
		java {
			srcDir "${buildDir.absolutePath}/generated/source/proto/main/grpc"
			srcDir "${buildDir.absolutePath}/generated/source/proto/main/java"
			srcDir "${buildDir.absolutePath}/generated/source/proto/main/reactor"
		}
	}
}

idea {
	module {
		generatedSourceDirs += file("${protobuf.generatedFilesBaseDir}/main/java")
		generatedSourceDirs += file("${protobuf.generatedFilesBaseDir}/main/grpc")
		generatedSourceDirs += file("${protobuf.generatedFilesBaseDir}/main/reactor")
	}
}

test {
	useJUnitPlatform()

	afterTest { desc, result ->
		logger.quiet "Executing test \"${desc.name}\" [${desc.className}] with result: ${result.resultType} and took: ${result.endTime - result.startTime}ms"
	}
}

integrationTest {
	useJUnitPlatform()

	afterTest { desc, result ->
		logger.quiet "Executing integration test \"${desc.name}\" [${desc.className}] with result: ${result.resultType} and took: ${result.endTime - result.startTime}ms"
	}
}

docker {
	def dockerName = "$project.group/$project.name"
	def dockerTag = project.version.toLowerCase()
	def jarFileName = bootJar.archiveFileName.get()

	name dockerName
	tag "latest", "$dockerName:$dockerTag"
	files bootJar.archiveFile.get()
	buildArgs([ jarFileName: jarFileName ])
	noCache true
}
